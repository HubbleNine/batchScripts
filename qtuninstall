@echo off

rem =================================================================================================================================
rem =================================================================================================================================
rem ***Name: QTUninstall.bat
rem ***Author: Me
rem ***Version: 1.0
rem ***Date: 04/18/2016
rem ***Information: This script will loop through all computer objects on the domain. It will check
rem 		    to see if Quicktime is installed and uninstall it. There is also a section to 
rem		    check for single computers at a time.
rem =================================================================================================================================
rem =================================================================================================================================

rem ***
rem ***In this start code group, the user is prompted with a choice of checking one computer or all computers on the domain,
rem ***if the user selects 1 or Y, the checkone group is called, if the user selects 2 or N, the checkall group is called.
rem ***

:start
choice /d 1 /n /c "YN12" /t 030 /m "Press 1 to check one computer, press 2 to check all domain."
if %errorlevel%==1 goto checkone
if %errorlevel%==2 goto checkall

rem ***
rem ***In this checkone code group, the user is prompted for a computer name and the single computer is checked to see if 
rem ***QuickTime is installed, if it is, it is promptly and silently uninstalled.
:checkone
echo Ready to check one!
set /p comp="Computer name:"
wmic /node: %comp% product where "name like 'QuickTime%%'" call uninstall /nointeractive
pause
choice /d N /t 030 /m "Do you want to check another computer?"
if %errorlevel%==1 goto checkone
if %errorlevel%==2 exit
if %errorlevel%==0 goto start
if %errorlevel% geq 3 goto end


:checkall
echo Ready to check all!
pause
rem ***This is the for loop, it will loop through *all* computers in the CBN.Local Forest
for /f "delims=" %%a in ('DSQUERY COMPUTER forestroot -o rdn -limit 0') do (
	call :checkallprocess %%a
)
goto :checkallend

:checkallprocess
set comp=%1
rem     ***Remove quotes surrounding computer name***
for /f "useback tokens=*" %%a in ('%comp%') do set comp=%%~a
wmic /node: %comp% product where "name like 'QuickTime%%'" call uninstall /nointeractive
goto :end

:checkallend
pause
choice /d N /t 030 /m "Do you want to check another computer?"
if %errorlevel%==1 goto checkone
if %errorlevel%==2 exit
if %errorlevel%==0 goto start
if %errorlevel% geq 3 goto end

:end
